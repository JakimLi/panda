apply plugin: 'java'


allprojects {
    group = 'com.github.jakimli.pandaria'
    version = '0.2.2'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'


    ext {
        springboot = '2.0.0.RELEASE'

        mysql = '5.1.6'

        cucumber = '4.0.0'

        jersey = '2.27'
        moco = '0.12.0'
        jsonpath = '2.4.0'
        junit = '4.12'
        guava = '19.0'
        joda_time = '2.10'

        commons_text = '1.4'

        json_schema_validator = '1.9.2'

        json_unit = '2.0.0.RC4'

        mongo = '3.8.2'

        faker = '0.16'
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name project.name
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'A lightweight API test framework'
                    url 'https://github.com/JakimLi/pandaria'

                    scm {
                        connection 'https://github.com/JakimLi/pandaria.git'
                        developerConnection 'git@github.com:JakimLi/pandaria.git'
                        url 'https://github.com/JakimLi/pandaria'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://github.com/JakimLi/pandaria/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'jakimli'
                            name 'Jakim Li'
                            email 'ljking7002@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '4.10.2'
}
