ext {
    archivesBaseName = "pandaria-core"
}

dependencies {
    compile(
            "io.cucumber:cucumber-java:${cucumber}",
            "io.cucumber:cucumber-spring:${cucumber}",

            // cucumber-spring has @txn hooks, which requires db related jars, cucumber-spring is required for
            // pandaria-core, so add db dependency here otherwise it will give NoClassDefError on TransactionDefinition.
            "org.springframework:spring-jdbc:${spring_jdbc}",

            "org.springframework.boot:spring-boot-starter-test:${springboot}",
            "org.springframework.boot:spring-boot-starter:${springboot}",
            "org.springframework.boot:spring-boot-starter-aop:${springboot}",

            "com.jayway.jsonpath:json-path:${jsonpath}",

            "net.javacrumbs.json-unit:json-unit-fluent:${json_unit}",
            "net.javacrumbs.json-unit:json-unit:${json_unit}",

            "junit:junit:${junit}",

            "com.google.guava:guava:${guava}",

            "joda-time:joda-time:${joda_time}",

            "org.apache.commons:commons-text:${commons_text}",

            "com.github.erosb:everit-json-schema:${json_schema_validator}",

            "org.glassfish.jersey.core:jersey-client:${jersey}",
            "org.glassfish.jersey.core:jersey-common:${jersey}",
            "org.glassfish.jersey.media:jersey-media-json-jackson:${jersey}",
            "org.glassfish.jersey.media:jersey-media-multipart:${jersey}",
            "org.glassfish.jersey.inject:jersey-hk2:${jersey}",

            "com.github.javafaker:javafaker:${faker}"
    )

    testCompile(
            "io.cucumber:cucumber-junit:${cucumber}",

            "com.github.dreamhead:moco-core:${moco}",
            "com.github.dreamhead:moco-runner:${moco}",
            "com.github.dreamhead:moco-junit:${moco}"
    )
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--tags', '~@ignore', '--glue', 'com.github.jakimli.pandaria', '--glue', 'cucumber.api.spring',
                    '--tags', 'not @ignore', 'src/test/resources']
        }
    }
}

build.dependsOn tasks.cucumber